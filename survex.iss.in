; @configure_input@
[Setup]
AppName=@PRETTYPACKAGE@
; AppVerName not required by InnoSetup >= 5.3.10
AppVerName=@PRETTYPACKAGE@ @VERSION@
DefaultDirName={pf32}\@PRETTYPACKAGE@
DefaultGroupName=@PRETTYPACKAGE@
OutputBaseFilename=@PACKAGE@-win32-@VERSION@
; FIXME translate this!
;AppComments=Survex Cave Surveying Software
AppPublisher=The Survex Project
AppPublisherURL=https://survex.com/
AppVersion=@VERSION@
VersionInfoTextVersion=@VERSION@
VersionInfoVersion=@VERSION@
ChangesAssociations=yes
LicenseFile=COPYING.txt
Uninstallable=yes
AppCopyright=@COPYRIGHT_MSG@
;WizardImageFile=survey.bmp
;WizardSmallImageFile=??? (up to 55x58)
LZMANumFastBytes=273
SolidCompression=yes
; Only show the "choose language" dialog if there isn't an exact match for
; the UI language the user has set.
ShowLanguageDialog=auto
;SetupIconFile=FIXME ; file in installation source directory
;UninstallDisplayIcon=FIXME
;AppReadmeFile=FIXME
;AppSupportURL=FIXME
;AppUpdatesURL=FIXME
AppContact=olly@survex.com
; Install file timestamps correctly!
TimeStampsInUTC=yes
; SignedUninstaller ???
; If upgrading, default to the language selected last time.
UsePreviousLanguage=yes
; If upgrading, install to the same location and use the same start menu folder.
DisableDirPage=auto
DisableProgramGroupPage=auto
; Disable the "welcome" page of the wizard.
DisableWelcomePage=yes

[Languages]
; Official translations:
Name: "ca"; MessagesFile: "compiler:Languages/Catalan.isl"
Name: "cs"; MessagesFile: "compiler:Languages/Czech.isl"
Name: "de"; MessagesFile: "compiler:Languages/German.isl"
Name: "en"; MessagesFile: "compiler:Default.isl"
Name: "el"; MessagesFile: "compiler:Languages/Greek.isl"
Name: "es"; MessagesFile: "compiler:Languages/Spanish.isl"
Name: "fr"; MessagesFile: "compiler:Languages/French.isl"
Name: "hu"; MessagesFile: "compiler:Languages/Hungarian.isl"
Name: "it"; MessagesFile: "compiler:Languages/Italian.isl"
Name: "pl"; MessagesFile: "compiler:Languages/Polish.isl"
Name: "pt"; MessagesFile: "compiler:Languages/Portuguese.isl"
Name: "pt_br"; MessagesFile: "compiler:Languages/BrazilianPortuguese.isl"
Name: "ru"; MessagesFile: "compiler:Languages/Russian.isl"
; Unofficial:
Name: "bg"; MessagesFile: "Bulgarian.isl"
Name: "en_gb"; MessagesFile: "EnglishBritish.isl"
Name: "id"; MessagesFile: "Indonesian.isl"
Name: "ro"; MessagesFile: "Romanian.isl"
Name: "sk"; MessagesFile: "Slovak.isl"
Name: "zh_CN"; MessagesFile: "ChineseSimplified.isl"
Name: "zh_TW"; MessagesFile: "ChineseTraditional.isl"

[Dirs]
Name: "{app}\manual"
Name: "{app}\images"

; Remove any cad3d.exe from a previous install - it has now been replaced
; by survexport.exe.
[InstallDelete]
Type: files; Name: "{app}\cad3d.exe"

[Files]
Source: "*.exe"; DestDir: "{app}"
; mingwm10.dll only needed if we use threads
;Source: "*.dll"; DestDir: "{app}"
Source: "*.svx"; DestDir: "{app}"
Source: "*.ico"; DestDir: "{app}"
Source: "*.pixelfont"; DestDir: "{app}"
Source: "*.png"; DestDir: "{app}\images"
Source: "*.msg"; DestDir: "{app}"
Source: "*.htm"; DestDir: "{app}"
Source: "manual\*.*"; DestDir: "{app}\manual"
; Generate iss file for including the wxstd.mo files
#include "i18nfiles.iss"
; Also install survexport.exe as 3dtopos.exe so existing Tunnel releases work.
Source: "survexport.exe"; DestDir: "{app}"; DestName: "3dtopos.exe"

; FIXME This should be translated (think there's a standard custommessage)
;[Run]
;Filename: "{app}\aven.exe"; Description: "Launch aven"; Flags: postinstall nowait skipifsilent unchecked

[Icons]
; Name: "{group}\SvxEdit"; Filename: "{app}\editwrap.exe"; Comment: "Enter survey data"
Name: "{group}\Aven"; Filename: "{app}\aven.exe"; Comment: "View processed surveys"
Name: "{group}\Documentation"; Filename: "{app}\index.htm"; Comment: "View the documentation for Survex"
Name: "{group}\{cm:UninstallProgram,@PRETTYPACKAGE@}"; Filename: "{uninstallexe}"

[Registry]
; Support for loading Compass Plot files
Root: HKCR; Subkey: ".plt"; ValueType: string; ValueData: "CompassPlotFile"
Root: HKCR; Subkey: ".plf"; ValueType: string; ValueData: "CompassPlotFile"
Root: HKCR; Subkey: "CompassPlotFile"; ValueType: string; ValueData: "Compass Plot File"
Root: HKCR; Subkey: "CompassPlotFile\DefaultIcon"; ValueType: string; ValueData:"{app}\plt.ico"
Root: HKCR; Subkey: "CompassPlotFile\shell\Open with Aven\command"; ValueType: string; ValueData: """{app}\aven.exe"" ""%1"""; Flags: uninsdeletekey
; Support for loading CMAP XYZ files
Root: HKCR; Subkey: ".xyz"; ValueType: string; ValueData: "CMAP.XYZ"
Root: HKCR; Subkey: "CMAP.XYZ"; ValueType: string; ValueData: "CMAP Processed Data"
;Root: HKCR; Subkey: "CMAP.XYZ\DefaultIcon"; ValueType: string; ValueData:"{app}\plt.ico"
Root: HKCR; Subkey: "CMAP.XYZ\shell\Open with Aven\command"; ValueType: string; ValueData: """{app}\aven.exe"" ""%1"""; Flags: uninsdeletekey
;
Root: HKCR; Subkey: ".svx"; ValueType: string; ValueData: "survex.source"; Flags: uninsdeletevalue
Root: HKCR; Subkey: ".3d"; ValueType: string; ValueData: "survex.bin"; Flags: uninsdeletevalue
Root: HKCR; Subkey: ".err"; ValueType: string; ValueData: "survex.error"; Flags: uninsdeletevalue
Root: HKCR; Subkey: ".pos"; ValueType: string; ValueData: "survex.pos"; Flags: uninsdeletevalue
Root: HKCR; Subkey: ".log"; ValueType: string; ValueData: "survex.log"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.source"; ValueType: string; ValueData: "Survex Raw Survey Data"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.source\DefaultIcon"; ValueType: string; ValueData: "{app}\svx.ico"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.source\shell\Open"; ValueType: none; Flags: deletekey
Root: HKCR; Subkey: "survex.source\shell\Process"; ValueType: none; Flags: deletekey
Root: HKCR; Subkey: "survex.source\shell\Open in Notepad\command"; ValueType: string; ValueData: """notepad.exe"" ""%1"""; Flags: uninsdeletevalue
;Root: HKCR; Subkey: "survex.source\shell\Open in SvxEdit\command"; ValueType: string; ValueData: """{app}\editwrap.exe"" ""%1"""; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.source\shell\Process\command"; ValueType: string; ValueData: """{app}\aven.exe"" ""%1"""; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.bin"; ValueType: string; ValueData: "Survex Processed Data"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.bin\DefaultIcon"; ValueType: string; ValueData:"{app}\3d.ico"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.bin\shell\Open\command"; ValueType: string; ValueData: """{app}\aven.exe"" ""%1"""; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.bin\shell\Print\command"; ValueType: string; ValueData: """{app}\aven.exe"" --print ""%1"""; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.bin\shell\Extend\command"; ValueType: string; ValueData: """{app}\extend.exe"" ""%1"""; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.error"; ValueType: string; ValueData: "Survex Loop Closure Errors"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.error\DefaultIcon"; ValueType: string; ValueData: "{app}\err.ico"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.error\shell\Open\command"; ValueType: string; ValueData: """notepad.exe"" ""%1"""; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.error\shell\Sort by Error\command"; ValueType: string; ValueData: """{app}\sorterr.exe"" -r ""%1"""; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.error\shell\Sort by Horizontal Error\command"; ValueType: string; ValueData: """{app}\sorterr.exe"" -r -h ""%1"""; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.error\shell\Sort by Vertical Error\command"; ValueType: string; ValueData: """{app}\sorterr.exe"" -r -v ""%1"""; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.error\shell\Sort by Percentage Error\command"; ValueType: string; ValueData: """{app}\sorterr.exe"" -r -p ""%1"""; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.error\shell\Sort by Error per Leg\command"; ValueType: string; ValueData: """{app}\sorterr.exe"" -r -l ""%1"""; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.pos"; ValueType: string; ValueData: "Survex Station Positions"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.pos\DefaultIcon"; ValueType: string; ValueData: "{app}\pos.ico"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.pos\shell\Open\command"; ValueType: string; ValueData: """notepad.exe"" ""%1"""; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.log"; ValueType: string; ValueData: "Output Log"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "survex.log\shell\Open\command"; ValueType: string; ValueData: """notepad.exe"" ""%1"""; Flags: uninsdeletevalue
; Remove old menu items which can now be better achieved via Aven.
Root: HKCR; Subkey: "survex.bin\shell\Convert to DXF\command"; ValueType: none; Flags: deletekey
Root: HKCR; Subkey: "survex.bin\shell\Convert for hand plotting\command"; ValueType: none; Flags: deletekey
